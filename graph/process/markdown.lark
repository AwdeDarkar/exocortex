// TERMINAL DEFINITIONS
%import common.DIGIT
%import common.INT
%import common.LETTER
%import common.CR
%import common.LF

_NEWLINE: CR? LF
ALPHANUM: DIGIT | LETTER
SAFE_PUNCTUATION: "!" | "\"" | "$" | "%" | "&" | "'" | "(" | ")" | "," | "-" | "." | ":" | ";" | "<" | "=" | ">" | "?" | "@" | "[" | "]" | "^" | "{" | "|" | "}" | "~"
RESERVED_PUNCTUATION: "#" | "*" | "_" | "+" | "\\" | "`" | "/"
ESCAPE.1: "`" RESERVED_PUNCTUATION
PUNCTUATION: SAFE_PUNCTUATION | RESERVED_PUNCTUATION
WHITESPACE: " " | "\t"
SAFECHAR: LETTER | SAFE_PUNCTUATION | ESCAPE
TEXTCHAR: ALPHANUM | PUNCTUATION

_RAW.5: "```"
RAW_CONTENT: /[\S\s]+?```/

WORD: SAFECHAR+
    | SAFECHAR TEXTCHAR* (SAFECHAR | DIGIT)
UNSAFE_WORD: TEXTCHAR+
LINE_START: TEXTCHAR | /[ \t]/
LINE: LINE_START TEXTCHAR+ _NEWLINE

_INDENT: "  "

// MAIN GRAMMAR
?start: root

// HEADINGS and SECTIONS
_heading_line: " "+ (WORD | UNSAFE_WORD) (WORD | UNSAFE_WORD | " ")*
             | WORD  (WORD | UNSAFE_WORD | " ")*
h1: "#" _heading_line
h2: "##" _heading_line
h3: "###" _heading_line
h4: "####" _heading_line
h5: "#####" _heading_line
h6: "######" _heading_line
hp: /#{7,}/ _heading_line

?root: _NEWLINE* (section_content | s1 | s2 | s3 | s4 | s5 | s6 | sp)*
s1: h1 _NEWLINE* (section_content | s2 | s3 | s4 | s5 | s6 | sp)*
s2: h2 _NEWLINE* (section_content | s3 | s4 | s5 | s6 | sp)*
s3: h3 _NEWLINE* (section_content | s4 | s5 | s6 | sp)*
s4: h4 _NEWLINE* (section_content | s5 | s6 | sp)*
s5: h5 _NEWLINE* (section_content | s6 | sp)*
s6: h6 _NEWLINE* (section_content | sp)*
sp: hp _NEWLINE* (section_content)*

?section_content: paragraph
                | block
                | list
                | raw_block
                | directive
                | comment
                | latex
                | hline

_NBLOCK_OPEN: "#_"
_NBLOCK_CLOSE: "_#"
_NBLOCK_END: "#_#"
block: _NBLOCK_OPEN [block_name] _NBLOCK_CLOSE _NEWLINE+ (section_content)* _NBLOCK_END _NEWLINE*
block_name: WORD

// PARAGRAPHS
_inline: strong | emphasis | " " | WORD

paragraph: (_NEWLINE? _inline)+ _paragraph_break?
_paragraph_break: _NEWLINE+ | "\\" _NEWLINE*

_HLINE: "---"
hline: _HLINE _NEWLINE*

// INLINE STYLE
_STRONG: "**" | "__"
_EMPHASIS: "_" | "*"
_words: (WORD | " " | _NEWLINE)+
strong: _STRONG _words _STRONG
emphasis: _EMPHASIS _words _EMPHASIS

// LISTS
indent: _INDENT
list_item{mark}: indent* mark _inline+ _NEWLINE?

ulist: ulist_item+ _NEWLINE*
_ULIST_MARK: "+" | "*"
?ulist_item: list_item{_ULIST_MARK}

olist: olist_item+
_OLIST_MARK: INT "."
?olist_item: list_item{_OLIST_MARK}

?list: ulist | olist

// RAW TEXT
format: WORD " "*
raw_block: _RAW " "* [format] _NEWLINE RAW_CONTENT _NEWLINE*

// DIRECTIVES
_DMARK.8: ".. "

directive_type: WORD
directive_data: WORD (WORD | " ")*
directive_block_line: _INDENT " " /.+/ _NEWLINE
directive_block: directive_block_line+
directive: _DMARK directive_type " ::" " "* [directive_data] _NEWLINE [directive_block] _NEWLINE*

// COMMENTS
_line_comment: "//" /./* _NEWLINE*
_block_comment: "/*" /[\S\s]+?\*\//  _NEWLINE*

comment: _line_comment | _block_comment

// LATEX
_INLINE_TEX: "\\("
_DISPLAY_TEX: "\\["

inline_tex: _INLINE_TEX /.+?\\\)/  _NEWLINE*
display_tex: _DISPLAY_TEX /[\S\s]+?\\\]/  _NEWLINE*

latex: inline_tex | display_tex
